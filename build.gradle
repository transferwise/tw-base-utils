import org.eclipse.jgit.api.errors.RefAlreadyExistsException

plugins {
    id "com.github.spotbugs" version "5.0.13"
    id "idea"
    id 'org.ajoberstar.grgit' version '5.0.0'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
}

idea.project {
    vcs = 'Git'
    languageLevel = JavaVersion.VERSION_11
    targetBytecodeVersion = JavaVersion.VERSION_11
}

ext.projectName = "TransferWise Base Utils"
ext.projectDescription = "TransferWise Base Utils"
ext.projectArtifactName = "tw-base-utils"

apply from: 'build.common.gradle'

dependencies {
    compileOnly libraries.springBootStarter
    compileOnly libraries.springTx
    compileOnly libraries.micrometerCore

    implementation libraries.commonsLang3
    implementation libraries.slf4jApi

    testImplementation libraries.awaitility
    testImplementation libraries.guava
    testImplementation libraries.junitJupiter
    testImplementation libraries.micrometerCore
    testImplementation libraries.spockCore
}

test {
    useJUnitPlatform()
}

task tagRelease {
    doLast {
        try {
            grgit.tag.add {
                name = "v$version"
                message = "Release of ${version}"
            }
            grgit.push(refsOrSpecs: ["v$version"])
        }
        catch (RefAlreadyExistsException ignored) {
            logger.warn("Tag v$version already exists.")
        }
    }
}

group = "com.transferwise.common"

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USER")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}