import org.eclipse.jgit.api.errors.RefAlreadyExistsException

plugins {
    id "com.github.spotbugs" version "4.7.0"
    id "idea"
    id 'org.ajoberstar.grgit' version '4.1.1'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
}

idea.project {
    vcs = 'Git'
    languageLevel = JavaVersion.VERSION_11
    targetBytecodeVersion = JavaVersion.VERSION_11
}

ext.projectName = "TransferWise Base Utils"
ext.projectDescription = "TransferWise Base Utils"
ext.projectArtifactName = "tw-base-utils"

apply from: 'build.common.gradle'

dependencies {
    compileOnly 'org.springframework.boot:spring-boot-starter'
    compileOnly 'org.springframework:spring-tx'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.5.2'
    compileOnly 'io.micrometer:micrometer-core'

    implementation 'org.slf4j:slf4j-api'
    implementation 'org.apache.commons:commons-lang3'

    testImplementation 'junit:junit'
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
    testImplementation 'org.awaitility:awaitility:4.1.1'
    testImplementation 'io.micrometer:micrometer-core'
    testImplementation 'org.assertj:assertj-core'

    testRuntimeOnly "org.junit.vintage:junit-vintage-engine"
}

test {
    useJUnitPlatform()
}

task tagRelease {
    doLast {
        try {
            grgit.tag.add {
                name = "v$version"
                message = "Release of ${version}"
            }
            grgit.push(refsOrSpecs: ["v$version"])
        }
        catch (RefAlreadyExistsException ignored) {
            logger.warn("Tag v$version already exists.")
        }
    }
}

group = "com.transferwise.common"

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USER")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}